Alphabet

A E I O P T C K th S H M N W Y
a e i o p t c k    s h m n w y ! only one theta

aa ee ii oo

%{n%}:n
%{k%}:k
%{o%}:o
%{tc%}:t
%{sth%}:th
%{e%}:e
%{voce%}:e
%{w%}:w
%{a%}:a
;

Sets

Con = p t c k th s h m n w y
      P T C K    S H M N W Y ;

NSCon = p t c k th h m n w y
        P T C K    H M N W Y ;

Vow = a e i o aa ee ii oo
      A E I O ;

LVow = aa ee ii oo ;

Rules

"Remove morpheme boundary"
  %^:0 <=> _ ;

"Remove hyphen starting dependent nouns"
  %-:0 <=> _ ;

"Insert -e between prefix and consonant"
  [%{n%}:ne | %{k%}:ke]  <=> _ %^: (%-:) Con: ;

"Prefixes modified before vowels in independent nouns"
  [%{n%}:net | %{k%}:ket | %{o%}:ot] <=> _ %^:0 Vow: ;

"i->e and o->oo between net/ket/ot and consonant"
  [i:e | o:oo] <=> [:net | :ket | :ot] %^: _ Con: ;

"o->0 before o in dependent nouns"
  %{o%}:0 <=> _ %^: %-: o: ;

"t/c to c before i"
  %{tc%}:c <=> _ %^: i: ;

"s/Î¸ to s before i"
  %{sth%}:s <=> _ %^: i: ;

"singular vocative deleted after final long vowel h"
  %{voce%}:0 <=> LVow: h: %^: _ ;

"final h drops between long weak vowel and singular vocative"
  h:0 <=> LVow: _ %^: %{voce%}: ;

"long weak vowel is shortened for singular vocative"
  [aa:a | ee:e | ii:i | oo:o] <=> _ h: %^: %{voce%}: ;

"vowel lengthens before G3 suffix or -wa"
  [a:aa | e:ee | i:ii | o:oo] <=> _ %^: [%{e%}: | %{w%}: ] ;

"G3 suffix loses e after vowel"
  %{e%}:0 <=> Vow: %^: _ ;

"vowel shortens before a G2 suffix"
  [aa:a | ee:e | ii:i | oo:o] <=> _ %^: %{a%}: ;

"we > o in some suffixes (e part)"
  %{e%}:o <=> w: %^: _ ;

"we > o in some suffixes (w part)"
  w:0 <=> _ %^: :e ;

"ww > o pt. 1"
  w:w <=> _ %^: :w ;

"ww > o pt. 2"
  w:0 <=> w: %^: _ ;

"y > 0 between s and -a"
  y:0 <=> s: _ %^: :a .#. ;

"y > 0 before i"
  y:0 <=> _ %^: :i ;

"ya > ie after consonant other than s pt. 1"
  y:i <=> NSCon: _  %^: [%{a%}: ] ;

"ya > ie after consonant other than s pt. 2"
  [%{a%}:e] <=> NSCon: y: %^: _ ;

"ya > ee after s or a pt. 1"
  y:e <=> [s: | a: ] _ %^: [%{a%}: ] \.#. ;

"ya > ee after s or a pt. 2"
  [%{a%}:e] <=> [s: | a: ] y: %^: _ ;

"ye and yw > i and iw after consonant other than s pt. 1"
  y:i <=> NSCon: _ %^: [%{e%}: | e: | %{voce%}: | :w] ;

"ye and yw > i and iw after consonant other than s pt. 2"
  [%{e%}:0 | e:0 | %{voce%}:0] <=> NSCon: y: %^: _ ;

"ye and yw > ii and iiw after s pt. 1"
  y:ii <=> s: _ %^: [%{e%}: | e:  | %{voce%}: | :w] ;

"ye and yw > ii and iiw after s pt. 2"
  [%{e%}:0 | e:0 | %{voce%}:0] <=> s: y: %^: _ ;
